apply plugin: 'java-gradle-plugin'

sourceSets {
    integrationTest {
        groovy.srcDir file( 'src/integTest/groovy' )
        resources.srcDir file( 'src/integTest/resources' )
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

idea {
    module {
        testSourceDirs += file( 'src/integTest/groovy' )
    }
}

task integrationTest( type: Test ) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test

    reports {
        html.destination = project.file( "$html.destination/integration" )
        junitXml.destination = project.file( "$junitXml.destination/integration" )
    }

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }
}

check.dependsOn integrationTest

gradlePlugin {
    testSourceSets sourceSets.integrationTest
}
